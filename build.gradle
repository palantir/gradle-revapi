import java.util.stream.Collectors

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:1.0.1'
        classpath 'com.netflix.nebula:gradle-info-plugin:9.1.1'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:16.0.0'
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'com.jfrog.bintray' version '1.8.5' apply false
    id 'com.palantir.baseline' version '3.37.1'
    id 'com.palantir.consistent-versions' version '1.26.1'
    id 'com.palantir.git-version' version '0.12.3'
    id 'org.inferred.processors' version '3.3.0'
}

repositories {
    jcenter()
    maven {
        url 'https://dl.bintray.com/palantir/releases/'
    }
}

group 'com.palantir.gradle.revapi'
version System.env.CIRCLE_TAG ?: gitVersion()

apply plugin: 'java-gradle-plugin'
apply plugin: 'java-library'
apply plugin: 'groovy'
apply from: "$rootDir/gradle/publish-jar.gradle"

sourceCompatibility = 1.8

dependencies {
    implementation 'org.revapi:revapi-basic-features'
    implementation 'org.revapi:revapi-java'
    implementation 'org.revapi:revapi-reporter-text'
    implementation 'org.freemarker:freemarker'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'

    testCompile platform('org.junit:junit-bom')
    testImplementation 'com.netflix.nebula:nebula-test'
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.assertj:assertj-core'
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine"

    annotationProcessor "org.immutables:value"
    annotationProcessor "org.immutables:serial"
    compileOnly "org.immutables:value::annotations"
    compileOnly "org.immutables:serial"
}

test {
    environment = System.getenv().entrySet().stream()
            .filter { entry -> entry.key != 'CIRCLE_TEST_REPORTS' }
            .collect(Collectors.toMap({ it.key }, { it.value }))
}

gradlePlugin {
    plugins {
        revapi {
            id = 'com.palantir.revapi'
            implementationClass = 'com.palantir.gradle.revapi.RevapiPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/palantir/gradle-revapi'
    vcsUrl = 'https://github.com/palantir/gradle-revapi'
    description = 'API/ABI backwards compatibility for Java libraries using Revapi'
    tags = ['versions']

    plugins {
        revapi {
            displayName = 'API/ABI backwards compatibility for Java libraries using Revapi'
        }
    }
allprojects {
    apply plugin: 'com.palantir.java-format'
}
}

tasks.publish.dependsOn tasks.publishPlugins
publishPlugins.onlyIf {
    versionDetails().isCleanTag
}
project.ext.'gradle.publish.key' = System.env["GRADLE_KEY"]
project.ext.'gradle.publish.secret' = System.env["GRADLE_SECRET"]

